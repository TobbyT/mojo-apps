# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
import("//build/config/mojo.gni")

import(rebase_path("mojo/public/tools/bindings/mojom.gni", ".", mojo_root))

group("apptest") {
  testonly = true

  deps = [
    ":apptests",
    ":service",
  ]
}

shared_library("service") {
  output_name = "example_service"

  sources = [
    "example_service_application.cc",
    "example_service_application.h",
    "example_service_impl.cc",
    "example_service_impl.h",
  ]

  deps = [
    ":bindings",
    rebase_path("mojo/public/c/system:for_shared_library", ".", mojo_root),
    rebase_path("mojo/public/cpp/application:standalone", ".", mojo_root),
    rebase_path("mojo/public/cpp/utility", ".", mojo_root)
  ]
}

shared_library("apptests") {
  output_name = "example_apptests"

  testonly = true

  sources = [
    "example_apptest.cc",
    "example_client_application.cc",
    "example_client_application.h",
    "example_client_impl.cc",
    "example_client_impl.h",
  ]

   deps = [
    ":bindings",
    rebase_path("mojo/public/c/system:for_shared_library", ".", mojo_root),
    rebase_path("mojo/public/cpp/application:standalone", ".", mojo_root),
    rebase_path("mojo/public/cpp/application:test_support_standalone", ".", mojo_root),
   ]

   datadeps = [ ":service", ]
}

mojom("bindings") {
  sources = [ "example_service.mojom" ]
}
